// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "../characters/interfaces/ICharacterOwner.sol";
import "./CharacterLevel.sol";

struct Activity {
    bool active;
    uint256 numberOfActivities;
    uint256 activityDuration;
    uint256 startBlock;
    uint256 endBlock;
}

/// @dev Farmland - Characters Health Smart Contract
contract CharacterActivity is CharacterLevel {

// CONSTRUCTOR

    constructor (
        address[3] memory farmlandAddresses       // Load key contract addresses
        ) CharacterLevel (farmlandAddresses)
    {
          require(farmlandAddresses.length == 3,      "Invalid number of contract addresses");
          require(farmlandAddresses[0] != address(0), "Invalid Character Contract address");
          require(farmlandAddresses[1] != address(0), "Invalid Character Slot Manager Contract address");
          require(farmlandAddresses[2] != address(0), "Invalid Character Owner Contract address");
    }

// STATE VARIABLES

    /// @dev A mapping to track a characters activity
    mapping(uint256 => Activity) public charactersActivity;
       
// EVENTS

    event ActivityInfoSet(address indexed account, uint256 tokenID, string typeSet, uint256 value);
    event SetActive(address indexed account, uint256 tokenID, bool active);
    event BeginActivitySet(address indexed account, uint256 tokenID, uint256 numberOfActivities, uint256 startBlock, uint256 endBlock); 
    event HealthReplenished(address indexed account, uint256 tokenID, uint256 health);

// FUNCTIONS

    /// @dev Update characters health 
    /// @param tokenID Characters ID
    /// @param active the amount
    function setActive(uint256 tokenID, bool active, address ownerOfCharacter)
        external
        onlyAllowed
        onlyCharacterOwner(tokenID, ownerOfCharacter)
    {
        emit SetActive(_msgSender(), tokenID, active);           // Write an event to the chain
        Activity storage activity = charactersActivity[tokenID]; // Shortcut to characters activity
        activity.active = active;
    }

    /// @dev Update characters charactersActivity duration
    /// @param tokenID Characters ID
    /// @param activityDuration the duration of the activity
    function setActivityDuration(uint256 tokenID, uint256 activityDuration, address ownerOfCharacter)
        external
        onlyAllowed
        onlyCharacterOwner(tokenID, ownerOfCharacter)
    {
        emit ActivityInfoSet(_msgSender(), tokenID, 'Activity Duration', activityDuration);   // Write an event to the chain
        Activity storage activity = charactersActivity[tokenID];                              // Shortcut to characters activity
        activity.activityDuration = activityDuration;
    }

    /// @dev Update characters Activity duration
    /// @param tokenID Characters ID
    /// @param startBlock the duration of the activity
    /// @param endBlock the duration of the activity
    /// @param numberOfActivities the of the activities
    function setBeginActivity(uint256 tokenID, uint256 numberOfActivities, uint256 startBlock, uint256 endBlock, address ownerOfCharacter)
        external
        onlyAllowed
        onlyCharacterOwner(tokenID, ownerOfCharacter)
    {
        require(endBlock > startBlock,                                                            "End block should be higher than start");
        emit BeginActivitySet(_msgSender(), tokenID, numberOfActivities, startBlock, endBlock);   // Write an event to the chain
        Activity storage activity = charactersActivity[tokenID];                                  // Shortcut to characters activity
        activity.startBlock = startBlock;
        activity.endBlock = endBlock;
        activity.numberOfActivities = numberOfActivities;
    }

//VIEWS
   
    /// @dev Return a characters current health
    /// @dev Health regenerates whilst a Character is resting (i.e., not on a activity)
    /// @dev character regains 1 stat per activity duration for that character 
    /// @dev so the speedier the character the quicker to regenerate
    /// @param tokenID Characters ID
    function calculateHealth(uint256 tokenID)
        external
        view
        returns (
            uint256 health
        )
    {
        Activity storage activity = charactersActivity[tokenID];                     // Shortcut to characters activity
        uint256 maxHealth = getCharactersMaxHealth(tokenID);                         // Get characters max health
        if (activity.endBlock == 0) {return maxHealth;}                              // If there's been no activity return max health
        health = getCharactersHealth(tokenID);                                       // Create local variable for health
        if (block.number <= activity.endBlock) {                                     // If activity not ended
            uint256 blockSinceStartOfActivity = block.number - activity.startBlock;  // Calculate blocks since activity started
            health -= blockSinceStartOfActivity / activity.activityDuration;         // Reduce health used = # of blocks since start of activity / # of Blocks to consume One Health stat
        } else {
            if (activity.active) {                                                   // If still active then
                health -= activity.numberOfActivities;                               // Reduce health by number of activities
            }
            uint256 blockSinceLastActivity = block.number - activity.endBlock;       // Calculate blocks since last activity finished
            health += blockSinceLastActivity / activity.activityDuration;            // Add health + health regenerated = # of blocks since last activity / # of Blocks To Regenerate One Health stat
            if (health > maxHealth) {return maxHealth;}                              // Ensure new energy amount doesn't exceed max health
        }
    }

    /// @dev Return the number of blocks until a characters health will regenerate
    /// @param tokenID Characters ID
    function getBlocksToMaxHealth(uint256 tokenID)
        external
        view
        returns (
            uint256 blocks
        )
    {
        Activity storage activity = charactersActivity[tokenID];                   // Shortcut to characters activity
        if (!activity.active) {                                                        // Character not on a activity
            uint256 blocksToMaxHealth = activity.endBlock + 
                            (activity.activityDuration * activity.numberOfActivities); // Calculate blocks until health is restored
            if (blocksToMaxHealth > block.number) {
                return blocksToMaxHealth - block.number;
            }
        }
    }

    /// @dev PUBLIC: Blocks remaining in activity, returns 0 if finished
    /// @param tokenID Characters ID
    function getBlocksUntilActivityEnds(uint256 tokenID)
        external
        view
        returns (
                uint256 blocksRemaining
        )
    {
        Activity storage activity = charactersActivity[tokenID];  // Shortcut to characters activity
        if (activity.endBlock > block.number) {
            return activity.endBlock - block.number;
        }
    }

}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/access/AccessControlEnumerable.sol";

contract Permissioned is AccessControlEnumerable {

    constructor () {
            _setupRole(DEFAULT_ADMIN_ROLE, _msgSender());
        }

// STATE VARIABLES

    /// @dev Defines the accessible roles
    bytes32 public constant ACCESS_ROLE = keccak256("ACCESS_ROLE");

// MODIFIERS

    /// @dev Only allows admin accounts
    modifier onlyOwner() {
        require(hasRole(DEFAULT_ADMIN_ROLE, _msgSender()), "Caller is not the owner");
        _; // Call the actual code
    }

    /// @dev Only allows accounts with permission
    modifier onlyAllowed() {
        require(hasRole(ACCESS_ROLE, _msgSender()), "Only addresses with permission");
        _; // Call the actual code
    }

// FUNCTIONS

  /// @dev Add an account to the access role. Restricted to admins.
  function addAllowed(address account)
    public virtual onlyOwner
  {
    grantRole(ACCESS_ROLE, account);
  }

  /// @dev Add an account to the admin role. Restricted to admins.
  function addOwner(address account)
    public virtual onlyOwner
  {
    grantRole(DEFAULT_ADMIN_ROLE, account);
  }

  /// @dev Remove an account from the access role. Restricted to admins.
  function removeAllowed(address account)
    public virtual onlyOwner
  {
    revokeRole(ACCESS_ROLE, account);
  }

  ///@dev Transfers ownership of the contract to a new account (`newOwner`). Can only be called by the current owner.
  function transferOwnership(address newOwner) 
      public virtual onlyOwner
  {
      require(newOwner != address(0), "Permissioned: new owner is the zero address");
      addOwner(newOwner);
      renounceOwner();
  }

  /// @dev Remove oneself from the owner role.
  function renounceOwner()
    public virtual
  {
    renounceRole(DEFAULT_ADMIN_ROLE, _msgSender());
  }

// VIEWS

  /// @dev Return `true` if the account belongs to the admin role.
  function isOwner(address account)
    public virtual view returns (bool)
  {
    return hasRole(DEFAULT_ADMIN_ROLE, account);
  }

  /// @dev Return `true` if the account belongs to the access role.
  function isAllowed(address account)
    public virtual view returns (bool)
  {
    return hasRole(ACCESS_ROLE, account);
  }

}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC721/extensions/IERC721Enumerable.sol";

struct CollectibleTraits {uint256 expiryDate; uint256 trait1; uint256 trait2; uint256 trait3; uint256 trait4; uint256 trait5;}
struct CollectibleSlots {uint256 slot1; uint256 slot2; uint256 slot3; uint256 slot4; uint256 slot5; uint256 slot6; uint256 slot7; uint256 slot8;}

abstract contract IFarmlandCollectible is IERC721Enumerable {

     /// @dev Stores the key traits for Farmland Collectibles
    mapping(uint256 => CollectibleTraits) public collectibleTraits;
    /// @dev Stores slots for Farmland Collectibles, can be used to store various items / awards for collectibles
    mapping(uint256 => CollectibleSlots) public collectibleSlots;
    function setCollectibleSlot(uint256 id, uint256 slotIndex, uint256 slot) external virtual;
    function walletOfOwner(address account) external view virtual returns(uint256[] memory tokenIds);

}

// SPDX-License-Identifier: MIT
pragma solidity >=0.8.0;

import "./IFarmlandCollectible.sol";

// Interface for the CharacterSlotManager  Farmland Characters
abstract contract ICharacterSlotManager {
    /// @dev Mapping for slots for Farmland Collectibles, can be used to store various stats or other persistent information
    mapping(uint256 => CollectibleSlots) public collectibleSlots;
    /// @dev Updates the slots values
    function updateSlot(uint256 tokenID, uint256 slotIndex, uint256 value) external virtual;
}

// SPDX-License-Identifier: MIT
pragma solidity >=0.8.0;

interface ICharacterOwner {
    function addCharacterContracts(address[] calldata characterContracts) external;
    function removeCharacterContract(address characterContract) external;
    function isAccountOwnerOfCharacter(address account, uint256 tokenID) external view returns (bool ownsCharacter);
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "../characters/interfaces/ICharacterSlotManager.sol";
import "../characters/interfaces/ICharacterOwner.sol";
import "./CharacterExperience.sol";

/// @dev Farmland - Characters Level Smart Contract
contract CharacterLevel is CharacterExperience {

// CONSTRUCTOR

    constructor (
          address[3] memory farmlandAddresses       // Load key contract addresses
        ) CharacterExperience (farmlandAddresses)
    {
        require(farmlandAddresses.length == 3,      "Invalid number of contract addresses");
    }

// EVENTS

    event LevelUp(address indexed account, uint256 tokenID, uint256 level);

// FUNCTIONS

    /// @dev Increase a characters level
    /// @param tokenID Characters ID
    function increaseLevel(uint256 tokenID, address ownerOfCharacter)
        public
        onlyAllowed
        onlyCharacterOwner(tokenID, ownerOfCharacter)
    {
        uint256 newLevel = getCharactersLevel(tokenID) + 1;        // Calculate the new level amount
        characterSlotManager.updateSlot(tokenID, 3, newLevel);     // Store the characters level
        emit LevelUp(_msgSender(), tokenID, newLevel);             // Write an event to the chain
    }

//VIEWS
   
    /// @dev Return a characters current level
    /// @param tokenID Characters ID
    function getCharactersLevel(uint256 tokenID)
        public
        view
        returns (
            uint256 level
        )
    {
        (,,level,,,,,) = farmlandCharacters.collectibleSlots(tokenID); // Retrieve Characters Slot 3 info
    }

}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "../utils/Permissioned.sol";
import "../characters/interfaces/ICharacterSlotManager.sol";
import "../characters/interfaces/ICharacterOwner.sol";

/// @dev Farmland - Characters Health Smart Contract
contract CharacterHealth is Permissioned {

// CONSTRUCTOR

    constructor (
          address[3] memory farmlandAddresses       // Load key contract addresses
        ) Permissioned()
        {
          require(farmlandAddresses.length == 3,      "Invalid number of contract addresses");
          require(farmlandAddresses[0] != address(0), "Invalid Character Contract address");
          require(farmlandAddresses[1] != address(0), "Invalid Character Slot Manager Contract address");
          require(farmlandAddresses[2] != address(0), "Invalid Character Owner Contract address");
          farmlandCharacters = IFarmlandCollectible(farmlandAddresses[0]);
          characterSlotManager = ICharacterSlotManager(farmlandAddresses[1]);
          characterOwner = ICharacterOwner(farmlandAddresses[2]);
        }

// STATE VARIABLES

    /// @dev The Farmland Character Owner Contract
    ICharacterOwner public immutable characterOwner;

    /// @dev The Farmland Character Contract
    IFarmlandCollectible public immutable farmlandCharacters;

    /// @dev The Farmland Character Slot Manager Contract
    ICharacterSlotManager public immutable characterSlotManager;

// EVENTS

    event HealthSet(address indexed account, uint256 tokenID, uint256 health);
    event HealthIncreased(address indexed account, uint256 tokenID, uint256 health);
    event HealthReduced(address indexed account, uint256 tokenID, uint256 health);

// MODIFIERS

    /// @dev Check if the character is owned by account calling function
    /// @param tokenID of character
    modifier onlyCharacterOwner (uint256 tokenID, address ownerOfCharacter) {
        require(farmlandCharacters.totalSupply() > tokenID,                         "Character doesn't exist");
        require(characterOwner.isAccountOwnerOfCharacter(ownerOfCharacter, tokenID),"You need to own or employ the Character");
        _;
    }

// FUNCTIONS

    /// @dev Set characters initial health based on character stats
    /// @param tokenID Characters ID
    function setInitialHealth(uint256 tokenID, address ownerOfCharacter)
        external
        onlyAllowed
        onlyCharacterOwner(tokenID, ownerOfCharacter)
    {
        require(getCharactersHealth(tokenID) == 0,               "Health already set");
        uint256 maxHealth = getCharactersMaxHealth(tokenID);     // Retrieve the characters max health
        characterSlotManager.updateSlot(tokenID, 1, maxHealth);  // Store the characters starting health
        emit HealthSet(_msgSender(), tokenID, maxHealth);        // Write an event to the chain
    }

    /// @dev Set characters initial health based on character stats
    /// @param tokenID Characters ID
    /// @param amount to add to the characters health
    function setHealthTo(uint256 tokenID, uint256 amount, address ownerOfCharacter)
        external
        onlyAllowed
        onlyCharacterOwner(tokenID, ownerOfCharacter)
    {
        uint256 maxHealth = getCharactersMaxHealth(tokenID);         // Retrieve the characters max health
        if ( amount > maxHealth) {                                   // Health can't exceed characters max
            characterSlotManager.updateSlot(tokenID, 1, maxHealth);  // Store the characters updated health
            emit HealthSet(_msgSender(), tokenID, maxHealth);        // Write an event to the chain
        } else {
            characterSlotManager.updateSlot(tokenID, 1, amount);     // Store the characters updated health
            emit HealthSet(_msgSender(), tokenID, amount);           // Write an event to the chain
        }
    }

    /// @dev Increase a characters health but don't exceed the max
    /// @param tokenID Characters ID
    /// @param amount to add to the characters health
    function increaseHealth(uint256 tokenID, uint256 amount, address ownerOfCharacter)
        external
        onlyAllowed
        onlyCharacterOwner(tokenID, ownerOfCharacter)
    {
        uint256 maxHealth = getCharactersMaxHealth(tokenID);         // Retrieve the characters max health
        uint256 currentHealth = getCharactersHealth(tokenID);        // Get current health
        require(currentHealth < maxHealth,                           "Character at max health");
        uint256 newHealth = currentHealth + amount;                  // Calculate the new health amount
        if ( newHealth > maxHealth) {                                // Replenished health can't exceed characters max
            characterSlotManager.updateSlot(tokenID, 1, maxHealth);  // Store the characters starting health
            emit HealthIncreased(_msgSender(), tokenID, maxHealth);  // Write an event to the chain
        } else {
            characterSlotManager.updateSlot(tokenID, 1, newHealth);  // Store the characters starting health
            emit HealthIncreased(_msgSender(), tokenID, newHealth);  // Write an event to the chain
        }
    }

    /// @dev Reduce a characters health but can't go below one
    /// @param tokenID Characters ID
    /// @param amount to add to the characters health
    function reduceHealth(uint256 tokenID, uint256 amount, address ownerOfCharacter)
        external
        onlyAllowed
        onlyCharacterOwner(tokenID, ownerOfCharacter)
    {
        uint256 currentHealth = getCharactersHealth(tokenID);    // Get current health
        require(currentHealth > amount,                          "Health too low");
        uint256 newHealth = currentHealth - amount;              // Calculate the new health amount
        characterSlotManager.updateSlot(tokenID, 1, newHealth);  // Store the characters health
        emit HealthReduced(_msgSender(), tokenID, newHealth);    // Write an event to the chain
    }

//VIEWS
   
    /// @dev Return a characters current health
    /// @param tokenID Characters ID
    function getCharactersHealth(uint256 tokenID)
        public
        view
        returns (
            uint256 health
        )
    {
        (health,,,,,,,) = farmlandCharacters.collectibleSlots(tokenID); // Retrieve Characters Slot 1 info
    }

    /// @dev Returns a characters default max health
    /// @param tokenID Characters ID
    function getCharactersMaxHealth(uint256 tokenID)
        public
        view
        returns (
            uint256 health
        )
    {
        // (,uint256 stamina, uint256 strength, uint256 speed, uint256 courage, uint256 intelligence ) = farmlandCharacters.collectibleTraits(tokenID);
        (,uint256 stamina, uint256 strength,,,) = farmlandCharacters.collectibleTraits(tokenID);  // Retrieve Explorer stats
        health = (strength + stamina) / 2;                                                        // Calculate the characters health
        if (tokenID < 100 || strength > 95 || stamina > 95) /// Founders, Tank or Warrior
        {
            health += health / 2;
        }
    }

}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "../characters/interfaces/ICharacterSlotManager.sol";
import "../characters/interfaces/ICharacterOwner.sol";
import "./CharacterHealth.sol";

/// @dev Farmland - Characters Experience Smart Contract
contract CharacterExperience is CharacterHealth {

// CONSTRUCTOR

    constructor (
          address[3] memory farmlandAddresses       // Load key contract addresses
        ) CharacterHealth (farmlandAddresses)
    {
        require(farmlandAddresses.length == 3,      "Invalid number of contract addresses");
    }

// EVENTS

    event ExperienceIncreased(address indexed account, uint256 tokenID, uint256 experience);
    event ExperienceReduced(address indexed account, uint256 tokenID, uint256 experience);

// FUNCTIONS

    /// @dev Increase a characters experience but don't exceed the max
    /// @param tokenID Characters ID
    /// @param amount to add to the characters experience
    function increaseExperience(uint256 tokenID, uint256 amount, address ownerOfCharacter)
        public
        onlyAllowed
        onlyCharacterOwner(tokenID, ownerOfCharacter)
    {
        uint256 newExperience = getCharactersExperience(tokenID) + amount;   // Calculate the new experience amount
        characterSlotManager.updateSlot(tokenID, 2, newExperience);          // Store the characters experience
        emit ExperienceIncreased(_msgSender(), tokenID, newExperience);      // Write an event to the chain
    }

    /// @dev Reduce a characters experience but can't go below one
    /// @param tokenID Characters ID
    /// @param amount to add to the characters experience
    function reduceExperience(uint256 tokenID, uint256 amount, address ownerOfCharacter)
        public
        onlyAllowed
        onlyCharacterOwner(tokenID, ownerOfCharacter)
    {
        require(getCharactersExperience(tokenID) > amount,                   "Experience too low");
        uint256 newExperience = getCharactersExperience(tokenID) - amount;   // Calculate the new experience amount
        characterSlotManager.updateSlot(tokenID, 2, newExperience);          // Store the characters experience
        emit ExperienceReduced(_msgSender(), tokenID, newExperience);        // Write an event to the chain
    }

//VIEWS
   
    /// @dev Return a characters current experience
    /// @param tokenID Characters ID
    function getCharactersExperience(uint256 tokenID)
        public
        view
        returns (
            uint256 experience
        )
    {
        (,experience,,,,,,) = farmlandCharacters.collectibleSlots(tokenID); // Retrieve Characters Slot 1 info
    }

}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/structs/EnumerableSet.sol)

pragma solidity ^0.8.0;

/**
 * @dev Library for managing
 * https://en.wikipedia.org/wiki/Set_(abstract_data_type)[sets] of primitive
 * types.
 *
 * Sets have the following properties:
 *
 * - Elements are added, removed, and checked for existence in constant time
 * (O(1)).
 * - Elements are enumerated in O(n). No guarantees are made on the ordering.
 *
 * ```
 * contract Example {
 *     // Add the library methods
 *     using EnumerableSet for EnumerableSet.AddressSet;
 *
 *     // Declare a set state variable
 *     EnumerableSet.AddressSet private mySet;
 * }
 * ```
 *
 * As of v3.3.0, sets of type `bytes32` (`Bytes32Set`), `address` (`AddressSet`)
 * and `uint256` (`UintSet`) are supported.
 */
library EnumerableSet {
    // To implement this library for multiple types with as little code
    // repetition as possible, we write it in terms of a generic Set type with
    // bytes32 values.
    // The Set implementation uses private functions, and user-facing
    // implementations (such as AddressSet) are just wrappers around the
    // underlying Set.
    // This means that we can only create new EnumerableSets for types that fit
    // in bytes32.

    struct Set {
        // Storage of set values
        bytes32[] _values;
        // Position of the value in the `values` array, plus 1 because index 0
        // means a value is not in the set.
        mapping(bytes32 => uint256) _indexes;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function _add(Set storage set, bytes32 value) private returns (bool) {
        if (!_contains(set, value)) {
            set._values.push(value);
            // The value is stored at length-1, but we add 1 to all indexes
            // and use 0 as a sentinel value
            set._indexes[value] = set._values.length;
            return true;
        } else {
            return false;
        }
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function _remove(Set storage set, bytes32 value) private returns (bool) {
        // We read and store the value's index to prevent multiple reads from the same storage slot
        uint256 valueIndex = set._indexes[value];

        if (valueIndex != 0) {
            // Equivalent to contains(set, value)
            // To delete an element from the _values array in O(1), we swap the element to delete with the last one in
            // the array, and then remove the last element (sometimes called as 'swap and pop').
            // This modifies the order of the array, as noted in {at}.

            uint256 toDeleteIndex = valueIndex - 1;
            uint256 lastIndex = set._values.length - 1;

            if (lastIndex != toDeleteIndex) {
                bytes32 lastvalue = set._values[lastIndex];

                // Move the last value to the index where the value to delete is
                set._values[toDeleteIndex] = lastvalue;
                // Update the index for the moved value
                set._indexes[lastvalue] = valueIndex; // Replace lastvalue's index to valueIndex
            }

            // Delete the slot where the moved value was stored
            set._values.pop();

            // Delete the index for the deleted slot
            delete set._indexes[value];

            return true;
        } else {
            return false;
        }
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function _contains(Set storage set, bytes32 value) private view returns (bool) {
        return set._indexes[value] != 0;
    }

    /**
     * @dev Returns the number of values on the set. O(1).
     */
    function _length(Set storage set) private view returns (uint256) {
        return set._values.length;
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function _at(Set storage set, uint256 index) private view returns (bytes32) {
        return set._values[index];
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function _values(Set storage set) private view returns (bytes32[] memory) {
        return set._values;
    }

    // Bytes32Set

    struct Bytes32Set {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(Bytes32Set storage set, bytes32 value) internal returns (bool) {
        return _add(set._inner, value);
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(Bytes32Set storage set, bytes32 value) internal returns (bool) {
        return _remove(set._inner, value);
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(Bytes32Set storage set, bytes32 value) internal view returns (bool) {
        return _contains(set._inner, value);
    }

    /**
     * @dev Returns the number of values in the set. O(1).
     */
    function length(Bytes32Set storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(Bytes32Set storage set, uint256 index) internal view returns (bytes32) {
        return _at(set._inner, index);
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function values(Bytes32Set storage set) internal view returns (bytes32[] memory) {
        return _values(set._inner);
    }

    // AddressSet

    struct AddressSet {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(AddressSet storage set, address value) internal returns (bool) {
        return _add(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(AddressSet storage set, address value) internal returns (bool) {
        return _remove(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(AddressSet storage set, address value) internal view returns (bool) {
        return _contains(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Returns the number of values in the set. O(1).
     */
    function length(AddressSet storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(AddressSet storage set, uint256 index) internal view returns (address) {
        return address(uint160(uint256(_at(set._inner, index))));
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function values(AddressSet storage set) internal view returns (address[] memory) {
        bytes32[] memory store = _values(set._inner);
        address[] memory result;

        assembly {
            result := store
        }

        return result;
    }

    // UintSet

    struct UintSet {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(UintSet storage set, uint256 value) internal returns (bool) {
        return _add(set._inner, bytes32(value));
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(UintSet storage set, uint256 value) internal returns (bool) {
        return _remove(set._inner, bytes32(value));
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(UintSet storage set, uint256 value) internal view returns (bool) {
        return _contains(set._inner, bytes32(value));
    }

    /**
     * @dev Returns the number of values on the set. O(1).
     */
    function length(UintSet storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(UintSet storage set, uint256 index) internal view returns (uint256) {
        return uint256(_at(set._inner, index));
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function values(UintSet storage set) internal view returns (uint256[] memory) {
        bytes32[] memory store = _values(set._inner);
        uint256[] memory result;

        assembly {
            result := store
        }

        return result;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/IERC165.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC165 standard, as defined in the
 * https://eips.ethereum.org/EIPS/eip-165[EIP].
 *
 * Implementers can declare support of contract interfaces, which can then be
 * queried by others ({ERC165Checker}).
 *
 * For an implementation, see {ERC165}.
 */
interface IERC165 {
    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30 000 gas.
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/ERC165.sol)

pragma solidity ^0.8.0;

import "./IERC165.sol";

/**
 * @dev Implementation of the {IERC165} interface.
 *
 * Contracts that want to implement ERC165 should inherit from this contract and override {supportsInterface} to check
 * for the additional interface id that will be supported. For example:
 *
 * ```solidity
 * function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
 *     return interfaceId == type(MyInterface).interfaceId || super.supportsInterface(interfaceId);
 * }
 * ```
 *
 * Alternatively, {ERC165Storage} provides an easier to use but more expensive implementation.
 */
abstract contract ERC165 is IERC165 {
    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IERC165).interfaceId;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Strings.sol)

pragma solidity ^0.8.0;

/**
 * @dev String operations.
 */
library Strings {
    bytes16 private constant _HEX_SYMBOLS = "0123456789abcdef";

    /**
     * @dev Converts a `uint256` to its ASCII `string` decimal representation.
     */
    function toString(uint256 value) internal pure returns (string memory) {
        // Inspired by OraclizeAPI's implementation - MIT licence
        // https://github.com/oraclize/ethereum-api/blob/b42146b063c7d6ee1358846c198246239e9360e8/oraclizeAPI_0.4.25.sol

        if (value == 0) {
            return "0";
        }
        uint256 temp = value;
        uint256 digits;
        while (temp != 0) {
            digits++;
            temp /= 10;
        }
        bytes memory buffer = new bytes(digits);
        while (value != 0) {
            digits -= 1;
            buffer[digits] = bytes1(uint8(48 + uint256(value % 10)));
            value /= 10;
        }
        return string(buffer);
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation.
     */
    function toHexString(uint256 value) internal pure returns (string memory) {
        if (value == 0) {
            return "0x00";
        }
        uint256 temp = value;
        uint256 length = 0;
        while (temp != 0) {
            length++;
            temp >>= 8;
        }
        return toHexString(value, length);
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation with fixed length.
     */
    function toHexString(uint256 value, uint256 length) internal pure returns (string memory) {
        bytes memory buffer = new bytes(2 * length + 2);
        buffer[0] = "0";
        buffer[1] = "x";
        for (uint256 i = 2 * length + 1; i > 1; --i) {
            buffer[i] = _HEX_SYMBOLS[value & 0xf];
            value >>= 4;
        }
        require(value == 0, "Strings: hex length insufficient");
        return string(buffer);
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (token/ERC721/extensions/IERC721Enumerable.sol)

pragma solidity ^0.8.0;

import "../IERC721.sol";

/**
 * @title ERC-721 Non-Fungible Token Standard, optional enumeration extension
 * @dev See https://eips.ethereum.org/EIPS/eip-721
 */
interface IERC721Enumerable is IERC721 {
    /**
     * @dev Returns the total amount of tokens stored by the contract.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns a token ID owned by `owner` at a given `index` of its token list.
     * Use along with {balanceOf} to enumerate all of ``owner``'s tokens.
     */
    function tokenOfOwnerByIndex(address owner, uint256 index) external view returns (uint256);

    /**
     * @dev Returns a token ID at a given `index` of all the tokens stored by the contract.
     * Use along with {totalSupply} to enumerate all tokens.
     */
    function tokenByIndex(uint256 index) external view returns (uint256);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC721/IERC721.sol)

pragma solidity ^0.8.0;

import "../../utils/introspection/IERC165.sol";

/**
 * @dev Required interface of an ERC721 compliant contract.
 */
interface IERC721 is IERC165 {
    /**
     * @dev Emitted when `tokenId` token is transferred from `from` to `to`.
     */
    event Transfer(address indexed from, address indexed to, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables `approved` to manage the `tokenId` token.
     */
    event Approval(address indexed owner, address indexed approved, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables or disables (`approved`) `operator` to manage all of its assets.
     */
    event ApprovalForAll(address indexed owner, address indexed operator, bool approved);

    /**
     * @dev Returns the number of tokens in ``owner``'s account.
     */
    function balanceOf(address owner) external view returns (uint256 balance);

    /**
     * @dev Returns the owner of the `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function ownerOf(uint256 tokenId) external view returns (address owner);

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`, checking first that contract recipients
     * are aware of the ERC721 protocol to prevent tokens from being forever locked.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If the caller is not `from`, it must be have been allowed to move this token by either {approve} or {setApprovalForAll}.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId
    ) external;

    /**
     * @dev Transfers `tokenId` token from `from` to `to`.
     *
     * WARNING: Usage of this method is discouraged, use {safeTransferFrom} whenever possible.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must be owned by `from`.
     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 tokenId
    ) external;

    /**
     * @dev Gives permission to `to` to transfer `tokenId` token to another account.
     * The approval is cleared when the token is transferred.
     *
     * Only a single account can be approved at a time, so approving the zero address clears previous approvals.
     *
     * Requirements:
     *
     * - The caller must own the token or be an approved operator.
     * - `tokenId` must exist.
     *
     * Emits an {Approval} event.
     */
    function approve(address to, uint256 tokenId) external;

    /**
     * @dev Returns the account approved for `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function getApproved(uint256 tokenId) external view returns (address operator);

    /**
     * @dev Approve or remove `operator` as an operator for the caller.
     * Operators can call {transferFrom} or {safeTransferFrom} for any token owned by the caller.
     *
     * Requirements:
     *
     * - The `operator` cannot be the caller.
     *
     * Emits an {ApprovalForAll} event.
     */
    function setApprovalForAll(address operator, bool _approved) external;

    /**
     * @dev Returns if the `operator` is allowed to manage all of the assets of `owner`.
     *
     * See {setApprovalForAll}
     */
    function isApprovedForAll(address owner, address operator) external view returns (bool);

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId,
        bytes calldata data
    ) external;
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (access/IAccessControlEnumerable.sol)

pragma solidity ^0.8.0;

import "./IAccessControl.sol";

/**
 * @dev External interface of AccessControlEnumerable declared to support ERC165 detection.
 */
interface IAccessControlEnumerable is IAccessControl {
    /**
     * @dev Returns one of the accounts that have `role`. `index` must be a
     * value between 0 and {getRoleMemberCount}, non-inclusive.
     *
     * Role bearers are not sorted in any particular way, and their ordering may
     * change at any point.
     *
     * WARNING: When using {getRoleMember} and {getRoleMemberCount}, make sure
     * you perform all queries on the same block. See the following
     * https://forum.openzeppelin.com/t/iterating-over-elements-on-enumerableset-in-openzeppelin-contracts/2296[forum post]
     * for more information.
     */
    function getRoleMember(bytes32 role, uint256 index) external view returns (address);

    /**
     * @dev Returns the number of accounts that have `role`. Can be used
     * together with {getRoleMember} to enumerate all bearers of a role.
     */
    function getRoleMemberCount(bytes32 role) external view returns (uint256);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (access/IAccessControl.sol)

pragma solidity ^0.8.0;

/**
 * @dev External interface of AccessControl declared to support ERC165 detection.
 */
interface IAccessControl {
    /**
     * @dev Emitted when `newAdminRole` is set as ``role``'s admin role, replacing `previousAdminRole`
     *
     * `DEFAULT_ADMIN_ROLE` is the starting admin for all roles, despite
     * {RoleAdminChanged} not being emitted signaling this.
     *
     * _Available since v3.1._
     */
    event RoleAdminChanged(bytes32 indexed role, bytes32 indexed previousAdminRole, bytes32 indexed newAdminRole);

    /**
     * @dev Emitted when `account` is granted `role`.
     *
     * `sender` is the account that originated the contract call, an admin role
     * bearer except when using {AccessControl-_setupRole}.
     */
    event RoleGranted(bytes32 indexed role, address indexed account, address indexed sender);

    /**
     * @dev Emitted when `account` is revoked `role`.
     *
     * `sender` is the account that originated the contract call:
     *   - if using `revokeRole`, it is the admin role bearer
     *   - if using `renounceRole`, it is the role bearer (i.e. `account`)
     */
    event RoleRevoked(bytes32 indexed role, address indexed account, address indexed sender);

    /**
     * @dev Returns `true` if `account` has been granted `role`.
     */
    function hasRole(bytes32 role, address account) external view returns (bool);

    /**
     * @dev Returns the admin role that controls `role`. See {grantRole} and
     * {revokeRole}.
     *
     * To change a role's admin, use {AccessControl-_setRoleAdmin}.
     */
    function getRoleAdmin(bytes32 role) external view returns (bytes32);

    /**
     * @dev Grants `role` to `account`.
     *
     * If `account` had not been already granted `role`, emits a {RoleGranted}
     * event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     */
    function grantRole(bytes32 role, address account) external;

    /**
     * @dev Revokes `role` from `account`.
     *
     * If `account` had been granted `role`, emits a {RoleRevoked} event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     */
    function revokeRole(bytes32 role, address account) external;

    /**
     * @dev Revokes `role` from the calling account.
     *
     * Roles are often managed via {grantRole} and {revokeRole}: this function's
     * purpose is to provide a mechanism for accounts to lose their privileges
     * if they are compromised (such as when a trusted device is misplaced).
     *
     * If the calling account had been granted `role`, emits a {RoleRevoked}
     * event.
     *
     * Requirements:
     *
     * - the caller must be `account`.
     */
    function renounceRole(bytes32 role, address account) external;
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (access/AccessControlEnumerable.sol)

pragma solidity ^0.8.0;

import "./IAccessControlEnumerable.sol";
import "./AccessControl.sol";
import "../utils/structs/EnumerableSet.sol";

/**
 * @dev Extension of {AccessControl} that allows enumerating the members of each role.
 */
abstract contract AccessControlEnumerable is IAccessControlEnumerable, AccessControl {
    using EnumerableSet for EnumerableSet.AddressSet;

    mapping(bytes32 => EnumerableSet.AddressSet) private _roleMembers;

    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IAccessControlEnumerable).interfaceId || super.supportsInterface(interfaceId);
    }

    /**
     * @dev Returns one of the accounts that have `role`. `index` must be a
     * value between 0 and {getRoleMemberCount}, non-inclusive.
     *
     * Role bearers are not sorted in any particular way, and their ordering may
     * change at any point.
     *
     * WARNING: When using {getRoleMember} and {getRoleMemberCount}, make sure
     * you perform all queries on the same block. See the following
     * https://forum.openzeppelin.com/t/iterating-over-elements-on-enumerableset-in-openzeppelin-contracts/2296[forum post]
     * for more information.
     */
    function getRoleMember(bytes32 role, uint256 index) public view virtual override returns (address) {
        return _roleMembers[role].at(index);
    }

    /**
     * @dev Returns the number of accounts that have `role`. Can be used
     * together with {getRoleMember} to enumerate all bearers of a role.
     */
    function getRoleMemberCount(bytes32 role) public view virtual override returns (uint256) {
        return _roleMembers[role].length();
    }

    /**
     * @dev Overload {_grantRole} to track enumerable memberships
     */
    function _grantRole(bytes32 role, address account) internal virtual override {
        super._grantRole(role, account);
        _roleMembers[role].add(account);
    }

    /**
     * @dev Overload {_revokeRole} to track enumerable memberships
     */
    function _revokeRole(bytes32 role, address account) internal virtual override {
        super._revokeRole(role, account);
        _roleMembers[role].remove(account);
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (access/AccessControl.sol)

pragma solidity ^0.8.0;

import "./IAccessControl.sol";
import "../utils/Context.sol";
import "../utils/Strings.sol";
import "../utils/introspection/ERC165.sol";

/**
 * @dev Contract module that allows children to implement role-based access
 * control mechanisms. This is a lightweight version that doesn't allow enumerating role
 * members except through off-chain means by accessing the contract event logs. Some
 * applications may benefit from on-chain enumerability, for those cases see
 * {AccessControlEnumerable}.
 *
 * Roles are referred to by their `bytes32` identifier. These should be exposed
 * in the external API and be unique. The best way to achieve this is by
 * using `public constant` hash digests:
 *
 * ```
 * bytes32 public constant MY_ROLE = keccak256("MY_ROLE");
 * ```
 *
 * Roles can be used to represent a set of permissions. To restrict access to a
 * function call, use {hasRole}:
 *
 * ```
 * function foo() public {
 *     require(hasRole(MY_ROLE, msg.sender));
 *     ...
 * }
 * ```
 *
 * Roles can be granted and revoked dynamically via the {grantRole} and
 * {revokeRole} functions. Each role has an associated admin role, and only
 * accounts that have a role's admin role can call {grantRole} and {revokeRole}.
 *
 * By default, the admin role for all roles is `DEFAULT_ADMIN_ROLE`, which means
 * that only accounts with this role will be able to grant or revoke other
 * roles. More complex role relationships can be created by using
 * {_setRoleAdmin}.
 *
 * WARNING: The `DEFAULT_ADMIN_ROLE` is also its own admin: it has permission to
 * grant and revoke this role. Extra precautions should be taken to secure
 * accounts that have been granted it.
 */
abstract contract AccessControl is Context, IAccessControl, ERC165 {
    struct RoleData {
        mapping(address => bool) members;
        bytes32 adminRole;
    }

    mapping(bytes32 => RoleData) private _roles;

    bytes32 public constant DEFAULT_ADMIN_ROLE = 0x00;

    /**
     * @dev Modifier that checks that an account has a specific role. Reverts
     * with a standardized message including the required role.
     *
     * The format of the revert reason is given by the following regular expression:
     *
     *  /^AccessControl: account (0x[0-9a-f]{40}) is missing role (0x[0-9a-f]{64})$/
     *
     * _Available since v4.1._
     */
    modifier onlyRole(bytes32 role) {
        _checkRole(role, _msgSender());
        _;
    }

    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IAccessControl).interfaceId || super.supportsInterface(interfaceId);
    }

    /**
     * @dev Returns `true` if `account` has been granted `role`.
     */
    function hasRole(bytes32 role, address account) public view virtual override returns (bool) {
        return _roles[role].members[account];
    }

    /**
     * @dev Revert with a standard message if `account` is missing `role`.
     *
     * The format of the revert reason is given by the following regular expression:
     *
     *  /^AccessControl: account (0x[0-9a-f]{40}) is missing role (0x[0-9a-f]{64})$/
     */
    function _checkRole(bytes32 role, address account) internal view virtual {
        if (!hasRole(role, account)) {
            revert(
                string(
                    abi.encodePacked(
                        "AccessControl: account ",
                        Strings.toHexString(uint160(account), 20),
                        " is missing role ",
                        Strings.toHexString(uint256(role), 32)
                    )
                )
            );
        }
    }

    /**
     * @dev Returns the admin role that controls `role`. See {grantRole} and
     * {revokeRole}.
     *
     * To change a role's admin, use {_setRoleAdmin}.
     */
    function getRoleAdmin(bytes32 role) public view virtual override returns (bytes32) {
        return _roles[role].adminRole;
    }

    /**
     * @dev Grants `role` to `account`.
     *
     * If `account` had not been already granted `role`, emits a {RoleGranted}
     * event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     */
    function grantRole(bytes32 role, address account) public virtual override onlyRole(getRoleAdmin(role)) {
        _grantRole(role, account);
    }

    /**
     * @dev Revokes `role` from `account`.
     *
     * If `account` had been granted `role`, emits a {RoleRevoked} event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     */
    function revokeRole(bytes32 role, address account) public virtual override onlyRole(getRoleAdmin(role)) {
        _revokeRole(role, account);
    }

    /**
     * @dev Revokes `role` from the calling account.
     *
     * Roles are often managed via {grantRole} and {revokeRole}: this function's
     * purpose is to provide a mechanism for accounts to lose their privileges
     * if they are compromised (such as when a trusted device is misplaced).
     *
     * If the calling account had been revoked `role`, emits a {RoleRevoked}
     * event.
     *
     * Requirements:
     *
     * - the caller must be `account`.
     */
    function renounceRole(bytes32 role, address account) public virtual override {
        require(account == _msgSender(), "AccessControl: can only renounce roles for self");

        _revokeRole(role, account);
    }

    /**
     * @dev Grants `role` to `account`.
     *
     * If `account` had not been already granted `role`, emits a {RoleGranted}
     * event. Note that unlike {grantRole}, this function doesn't perform any
     * checks on the calling account.
     *
     * [WARNING]
     * ====
     * This function should only be called from the constructor when setting
     * up the initial roles for the system.
     *
     * Using this function in any other way is effectively circumventing the admin
     * system imposed by {AccessControl}.
     * ====
     *
     * NOTE: This function is deprecated in favor of {_grantRole}.
     */
    function _setupRole(bytes32 role, address account) internal virtual {
        _grantRole(role, account);
    }

    /**
     * @dev Sets `adminRole` as ``role``'s admin role.
     *
     * Emits a {RoleAdminChanged} event.
     */
    function _setRoleAdmin(bytes32 role, bytes32 adminRole) internal virtual {
        bytes32 previousAdminRole = getRoleAdmin(role);
        _roles[role].adminRole = adminRole;
        emit RoleAdminChanged(role, previousAdminRole, adminRole);
    }

    /**
     * @dev Grants `role` to `account`.
     *
     * Internal function without access restriction.
     */
    function _grantRole(bytes32 role, address account) internal virtual {
        if (!hasRole(role, account)) {
            _roles[role].members[account] = true;
            emit RoleGranted(role, account, _msgSender());
        }
    }

    /**
     * @dev Revokes `role` from `account`.
     *
     * Internal function without access restriction.
     */
    function _revokeRole(bytes32 role, address account) internal virtual {
        if (hasRole(role, account)) {
            _roles[role].members[account] = false;
            emit RoleRevoked(role, account, _msgSender());
        }
    }
}